cmake_minimum_required(VERSION 3.10.0)

project(googlebenchmark-download NONE)

# Enable ExternalProject CMake module
include(ExternalProject)

option(ALLOW_DOWNLOADING_GOOGLEBENCHMARK "If GOOGLEBENCHMARK src tree is not found in location specified by GOOGLEBENCHMARK_PATH, do fetch the archive from internet" OFF)
set(GOOGLEBENCHMARK_PATH "/usr/src/googlebenchmark" CACHE PATH "Path to the googlebenchmark root tree.")

# Download and install GOOGLEBENCHMARK

message(STATUS "Looking for GOOGLEBENCHMARK sources")
message(STATUS "Looking for GOOGLEBENCHMARK sources in ${GOOGLEBENCHMARK_PATH}")
if(EXISTS "${GOOGLEBENCHMARK_PATH}" AND IS_DIRECTORY "${GOOGLEBENCHMARK_PATH}" AND EXISTS "${GOOGLEBENCHMARK_PATH}/CMakeLists.txt")
  message(STATUS "Found GOOGLEBENCHMARK in ${GOOGLEBENCHMARK_PATH}")

  ExternalProject_Add(
    googlebenchmark
    PREFIX            "${CMAKE_BINARY_DIR}"
    DOWNLOAD_DIR      "${CMAKE_BINARY_DIR}/download"
    SOURCE_DIR        "${GOOGLEBENCHMARK_PATH}" # use existing src dir.
    BINARY_DIR        "${CMAKE_BINARY_DIR}/build"
    CONFIGURE_COMMAND ""
    BUILD_COMMAND     ""
    INSTALL_COMMAND   ""
    TEST_COMMAND      ""
  )
else()
  if(NOT ALLOW_DOWNLOADING_GOOGLEBENCHMARK)
    message(SEND_ERROR "Did not find GOOGLEBENCHMARK sources! Either pass correct path in GOOGLEBENCHMARK_PATH, or enable ALLOW_DOWNLOADING_GOOGLEBENCHMARK, or disable BUILD_BENCHMARKING.")
  else()
    message(WARNING "Did not find GOOGLEBENCHMARK sources! Fetching from web...")
    ExternalProject_Add(
      googlebenchmark
      URL               https://github.com/google/benchmark/archive/v1.5.4.tar.gz
      URL_HASH          SHA512=3271cfe1b5db8c1790e756bbed5941647c46b17efbede910659a2b067830c5361b29b7fce7a3f1b290498c4e5a0da7e159bddd203b93e3ee38ceea72bcc62e0e
      PREFIX            "${CMAKE_BINARY_DIR}"
      STAMP_DIR         "${CMAKE_BINARY_DIR}/stamp"
      DOWNLOAD_DIR      "${CMAKE_BINARY_DIR}/download"
      SOURCE_DIR        "${CMAKE_BINARY_DIR}/src"
      BINARY_DIR        "${CMAKE_BINARY_DIR}/build"
      CONFIGURE_COMMAND ""
      BUILD_COMMAND     ""
      INSTALL_COMMAND   ""
      TEST_COMMAND      ""
    )
  endif()
endif()

ExternalProject_Get_Property(googlebenchmark SOURCE_DIR BINARY_DIR)
file(WRITE googlebenchmark-paths.cmake
"set(GOOGLEBENCHMARK_SOURCE_DIR \"${SOURCE_DIR}\")
set(GOOGLEBENCHMARK_BINARY_DIR \"${BINARY_DIR}\")
")
